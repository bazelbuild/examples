module(
    name = "example",
    version = "0.0.1",
)

bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "abseil-cpp", version = "20250814.1", repo_name = "com_google_absl")

# # Override abseil-cpp to a fork version with archive_override.
archive_override(
    module_name = "abseil-cpp",
    integrity = "sha256-nfNEp86X1YI48kp7nyCFbubyFrtzDw+8dwBl0PACXrc=",
    strip_prefix = "abseil-cpp-7599e36e7cbad38ec77cadd959d3a45d2124800a",
    urls = ["https://github.com/abseil/abseil-cpp/archive/7599e36e7cbad38ec77cadd959d3a45d2124800a.tar.gz"],
)

# Override platforms to a fork version with git_override.
# platforms is still an indirect dependency, the override itself doesn't give the root module visibility on platforms.
git_override(
    module_name = "platforms",
    commit = "5cf94563e35494b0dab15435868dd7f9e3cab2c8",
    remote = "https://github.com/bazelbuild/platforms.git",
)

# Patch bazel skylib 1.9.0 with a local patch file.
bazel_dep(name = "bazel_skylib", version = "1.9.0")
single_version_override(
    module_name = "bazel_skylib",
    patch_strip = 1,
    patches = ["//:bazel_skylib.patch"],
    version = "1.9.0",
)

# Depend on module lib_a at a local path.
# The bazel_dep statement is necessary because the root module must have visibility on lib_a.
bazel_dep(name = "lib_a", version = "")
local_path_override(
    module_name = "lib_a",
    path = "./lib_a",
)

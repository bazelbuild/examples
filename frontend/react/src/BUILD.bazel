load("@aspect_rules_jest//jest:defs.bzl", "jest_test")
load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@aspect_rules_swc//swc:defs.bzl", "swc")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@bazel_skylib//lib:partial.bzl", "partial")
load("//:lint.bzl", "eslint_test")

ASSET_PATTERNS = [
    "*.svg",
    "*.css",
]

SRC_PATTERNS = [
    "*.tsx",
    "*.ts",
]

TRANSPILER = partial.make(
    swc,
    swcrc = "//react:.swcrc",
)

# Filename conventions described at
# https://create-react-app.dev/docs/running-tests#filename-conventions
TEST_PATTERNS = [
    "*.test.tsx",
    "*.test.ts",
    "*.spec.tsx",
    "*.spec.ts",
]

js_library(
    name = "src",
    srcs = glob(ASSET_PATTERNS),
    visibility = ["//visibility:public"],
    deps = [
        ":src_ts",
    ],
)

ts_project(
    name = "src_ts",
    srcs = glob(
        include = SRC_PATTERNS,
        exclude = TEST_PATTERNS,
    ),
    declaration = True,
    resolve_json_module = True,
    transpiler = TRANSPILER,
    tsconfig = "//react:tsconfig",
    deps = [
        "//react:node_modules/react",
        "//react:node_modules/react-dom",
        "//react:node_modules/web-vitals",
    ],
)

ts_project(
    name = "src_test_ts",
    srcs = glob(TEST_PATTERNS),
    declaration = True,
    resolve_json_module = True,
    transpiler = TRANSPILER,
    tsconfig = "//react:tsconfig",
    deps = [
        ":src_ts",
        "//react:node_modules/@testing-library/jest-dom",
        "//react:node_modules/@testing-library/react",
        "//react:node_modules/@types/jest",
    ],
)

jest_test(
    name = "src_test",
    config = "//react:jest_config",
    data = [
        ":src",
        ":src_test_ts",
        "//react:node_modules/jest-environment-jsdom",
        "//react:node_modules/jest-transform-stub",
        "//react:node_modules/react",
    ],
    node_modules = "//react:node_modules",
)

# Test that fails if the lint report is non-empty
# Remove the `eslint-disable-next-line` line from index.tsx to see this test fail.
eslint_test(
    name = "lint",
    timeout = "short",
    srcs = [":src_ts_typings"],
)
